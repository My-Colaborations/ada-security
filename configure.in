dnl Autoconf configure script for Ada Security
dnl Copyright (C) 2012 Stephane Carrez
dnl Written by Stephane Carrez (Stephane.Carrez@gmail.com)
dnl
dnl  Licensed under the Apache License, Version 2.0 (the "License");
dnl  you may not use this file except in compliance with the License.
dnl  You may obtain a copy of the License at
dnl
dnl     http://www.apache.org/licenses/LICENSE-2.0
dnl
dnl  Unless required by applicable law or agreed to in writing, software
dnl  distributed under the License is distributed on an "AS IS" BASIS,
dnl  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
dnl  See the License for the specific language governing permissions and
dnl  limitations under the License.
dnl 
dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/security.ads)

AC_CANONICAL_SYSTEM
dnl AM_MAINTAINER_MODE

# Current release settings
SECURITY_MAJOR_VERSION=0
SECURITY_MINOR_VERSION=1
SECURITY_MICRO_VERSION=0
SECURITY_VERSION=$SECURITY_MAJOR_VERSION.$SECURITY_MINOR_VERSION.$SECURITY_MICRO_VERSION

AC_CHECK_PROGS(GNATMAKE, gnatmake, "")

# checking for local tools
AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PROG_LN_S

# Set the version number of Ada Server Faces
AC_SUBST(SECURITY_VERSION)
AC_SUBST(SECURITY_MAJOR_VERSION)
AC_SUBST(SECURITY_MINOR_VERSION)
AC_SUBST(SECURITY_MICRO_VERSION)

EXEC_PREFIX="$prefix"
AC_SUBST(EXEC_PREFIX)

SECURITY_LIBDIR="lib"
AC_SUBST(SECURITY_LIBDIR)

##########################################
# Check to enable/disable shared library
##########################################

AC_MSG_CHECKING([library type])
ac_enable_shared=no
AC_ARG_ENABLE(shared,
[  --enable-shared         Enable the shared libraries (disabled)],
[case "${enableval}" in
  no|none)  ac_enable_shared=no ;;
  *)        ac_enable_shared=yes ;;
esac])dnl

AC_MSG_RESULT(${ac_enable_shared})
BUILDS_SHARED=$ac_enable_shared
AC_SUBST(BUILDS_SHARED)

##########################################
# Ada Util library
##########################################

AC_MSG_CHECKING([Ada Utility library (code.google.com/p/ada-util)])
AC_ARG_WITH(ada-util,
AS_HELP_STRING([--with-ada-util=x], [Path for the Ada Utility library (code.google.com/p/ada-util)]),
[
  UTIL_DIR=${withval}/
],
[
  echo "with \"util\"; project t is for Source_Dirs use (); end t;" > t.gpr
  gnatmake -p -Pt >/dev/null 2>/dev/null
  if test $? -eq 0; then
    UTIL_DIR=
  else
    UTIL_DIR=../util/
  fi;
  rm -f t.gpr
])
echo "with \"${UTIL_DIR}util\"; project t is for Source_Dirs use (); end t;" > t.gpr
gnatmake -p -Pt >/dev/null 2>/dev/null
if test $? -ne 0 ; then
   rm -f t.gpr
   AC_MSG_ERROR([Ada Utility library not found in ${UTIL_DIR}])
fi
rm -f t.gpr
AC_MSG_RESULT(${UTIL_DIR})
AC_SUBST(UTIL_DIR)

AC_OUTPUT(
Makefile security.gpr tests.gpr distrib/security.gpr
)
